		device	zxspectrum128

	ifdef	isFX
		display	"Compiling 'lucyshow'"
	endif

		include	"../kernel/constants.inc"

;;-----------------------------------------------------------------------------
;; Busy soft ;; 03.03.2016 - 06.03.2016 ;; Sposoby objavenia a miznutia screenu


		org	RUNZONE

start		ei
		halt
		ld	hl,#5800
		ld	de,#5801
		ld	bc,#02FF
		ld	(hl),0q066
		ldir
		ld	hl,screen
		ld	d,#40
		ld	b,#18
		ldir
		ld	a,#10
		call	xchg.bnk

		ld	hl,taptap.start
		ld	de,taptap
		ld	bc,taptap.length
		ldir
		ld	a,#CD
		ld	hl,taptap
		ld	(IMCALL+1),hl
		ld	(IMCALL),a

		call	init

fx_start	ld	a,#3E
fx_loop		call	frame
		ex	af,af'
		ld	a,(#5AFF)
		rrca
		rrca
		rrca
		and	7
		ld	(actborder),a
		ex	af,af'
		inc	a
fx_end		cp	#FE
		jr	nz,fx_loop

fx_finish	ld	bc,5520
		jp	waitforit

frame		ld	xh,a
		push	af
		halt
		di			;; DI kvoli pristupu
		ld	(backsp+1),sp	;; k datam cez zasobnik
		ld	sp,vzoatt
		ld	bc,#5800	;; Hlavny engine zaberie
		ld	h,high tabsel	;; cca 40000 taktov
		jp	rutexe		;; takze za prerusenie
backsp		ld	sp,#5555	;; sa v pohode stiha
		ei
		pop	af
		ret

;; initialization / generators
init		ld	hl,shades
		ld	de,tabmul
		ld	bc,#0100
		ldir

makvza		ld	hl,scratt
		ld	de,pattern
		ld	bc,vzoatt
mkvzll		ld	a,(hl)
		ld	(bc),a
		inc	hl
		inc	bc
		ld	a,(de)
		ld	(bc),a
		inc	de
		inc	bc
		ld	a,h
		cp	high scratt + #03
		jr	c,mkvzll

		ld	hl,tabsel+#80
		ld	de,tabsel+#7F
		ld	bc,#0F10 + high tabpal
		ld	a,high tabpal + #0F
mksel1		ld	(hl),c
		ld	(de),a
		dec	a
		dec	e
		inc	l
		inc	c
		djnz	mksel1
mksel2		ld	(hl),c
		ld	(de),a
		dec	e
		inc	l
		jr	nz,mksel2

		ld	hl,tabpal
		ld	d,high tabmul
mkpal1		ld	a,h
		sub	high tabpal
		add	a,a
		add	a,a
		add	a,a
		ld	e,a
		ld	a,l
		and	#07
		or	e
		ld	e,a
		ld	a,(de)
		ld	c,a	;; INK
		ld	a,e
		and	#F8
		ld	e,a
		ld	a,l
		rrca
		rrca
		rrca
		and	#07
		or	e
		ld	e,a
		ld	a,(de)
		add	a,a
		add	a,a
		add	a,a
		or	c
		ld	(hl),a	;; PAPER
		inc	l
		bit	6,l
		jr	z,mkpal1
		ld	c,l
		ld	b,h
		ld	l,#00
mkpal2		ld	a,c
		and	#C0	;; BRIGHT FLASH
		or	(hl)
		ld	(bc),a
		inc	l
		inc	c
		res	6,l
		jr	nz,mkpal2
		inc	h
		ld	a,h
		cp	high tabpal + #20
		jr	c,mkpal1

makrut		ld	hl,rutzac
		ld	de,rutexe
		ld	bc,rutlen
		push	de
		ldir
		pop	hl
		ld	bc,rutlen * #2FF
		ldir
		ex	de,hl
		ld	(hl),#C3
		inc	hl
		ld	(hl),low backsp
		inc	hl
		ld	(hl),high backsp

		ld	hl,pattern
		ld	de,#00FF	;; D=max,E=min
minmax		ld	a,(hl)
		cp	d
		jr	c,mmxx11
		ld	d,a
mmxx11		cp	e
		jr	nc,mmxx22
		ld	e,a
mmxx22		inc	hl
		ld	a,h
		cp	high pattern + #03
		jr	c,minmax
		ld	a,#70
		sub	d
		ld	(fx_start+1),a
		ld	a,#90
		sub	e
		ld	(fx_end+1),a

		ret

rutzac		pop	de		;; D=vzor, E=original
		ld	a,xh
		add	a,d		;; Faza
		ld	l,a
		ld	d,(hl)		;; tabsel
		ld	a,(de)		;; tabpal
		ld	(bc),a		;; Atributy na obrazovke
		inc	bc		;; \ 51 T
rutlen = $-rutzac

@taptap.start
	phase #7E00

SPEED = 8

;; Lucy eyes and hands
taptap		ld	a,(FRAMES)
		and	127
		cp	9
		jr	nc,.end
		ld	l,a
		ld	h,0
		ld	de,lucyeyes_table
		add	hl,de
		ld	l,(hl)
		ld	h,0
		ld	de,lucyeyes
		add	hl,de
		ld	de,4970h
		ld	a,4
.loop		ldi
		inc	e
		ldi
		dec	e
		dec	e
		dec	e
		inc	d
		dec	a
		jr	nz,.loop
.end

taptik		ld	a,SPEED
		dec	a
		ld	(taptik+1),a
		ret	nz
		ld	a,SPEED
		ld	(taptik+1),a

taptik2		ld	a,0
		inc	a
		cp	24
		jr	nz,.ok
		xor	a
.ok		ld	(taptik2+1),a

		add	a,a
		ld	e,a
		ld	d,0
		ld	hl,lucyhands_anim
		add	hl,de
		ld	a,(hl)
		exx

		add	a,a
		ld	e,a
		ld	d,0
		ld	hl,lucyhands_tab
		add	hl,de
		ld	a,(hl)
		inc	hl
		ld	h,(hl)
		ld	l,a
		ld	de,48EFh
		call	view_spr3

		exx
		inc	hl
		ld	a,(hl)
		exx
		add	a,a
		ld	e,a
		ld	d,0
		ld	hl,lucyhands_tab+4
		add	hl,de
		ld	a,(hl)
		inc	hl
		ld	h,(hl)
		ld	l,a
		ld	de,48EFh+3

view_spr2	ld	a,16
.loop		ex	af,af'
		ld	a,e
		ldi
		ldi
		ld	e,a
		call	downde
		ex	af,af'
		dec	a
		jr	nz,.loop
		ret

view_spr3	ld	a,16
.loop		ex	af,af'
		ld	a,e
		ldi
		ldi
		ldi
		ld	e,a
		call	downde
		ex	af,af'
		dec	a
		jr	nz,.loop
		ret

lucyeyes_table	db	0, 10, 10, 20, 20, 20, 10, 10, 0

lucyeyes	db	00001100b, 01100000b
		db	00011110b, 11110000b
		db	00011110b, 11110000b
		db	00011110b, 11110000b
		db	00001100b, 01100000b

		db	00000000b, 00000000b
		db	00000000b, 00000000b
		db	00001100b, 01100000b
		db	00011110b, 11110000b
		db	00001100b, 01100000b

		db	00000000b, 00000000b
		db	00000000b, 00000000b
		db	00000000b, 00000000b
		db	00000000b, 00000000b
		db	00001100b, 01100000b

lucyhands_anim	db	1,0	; 1
		db	0,0	; 2
		db	0,0	; 3
		db	0,0	; 4
		db	0,0	; 5
		db	0,1	; 6
		db	0,0	; 7
		db	1,1	; 8
		db	0,0	; 9
		db	1,0	;10
		db	0,0	;11
		db	0,1	;12
		db	0,0	;13
		db	0,1	;14
		db	0,0	;15
		db	1,0	;16
		db	0,0	;17
		db	1,1	;18
		db	0,0	;19
		db	0,1	;20
		db	0,0	;21
		db	0,0	;22
		db	0,0	;23
		db	1,1	;24

lucyhands_tab	dw	lucyhands_spr
		dw	lucyhands_spr+80
		dw	lucyhands_spr+48
		dw	lucyhands_spr+48+80

lucyhands_spr	incbin "lucyhands.bin"

	dephase

@taptap.length = $ - taptap.start


		;;	0-1-2-3-4-5-6-7
shades
		db	6,6,6,6,6,6,6,6
		db	6,6,6,6,6,6,6,6
		db	7,6,6,6,6,6,6,6
		db	7,6,6,6,6,6,6,6
		db	7,5,6,6,6,6,6,6
		db	6,5,6,6,6,6,6,6
		db	6,5,5,6,6,6,6,6
		db	5,5,5,6,6,6,6,6
		db	5,4,5,5,5,6,6,6
		db	5,4,5,5,5,6,6,6
		db	5,4,5,5,5,6,6,6
		db	5,4,5,5,5,6,6,6
		db	4,4,4,5,5,6,6,6
		db	4,4,4,5,5,6,6,6
		db	4,3,4,5,5,6,6,6
		db	4,3,4,5,5,6,6,6
		db	4,3,4,4,5,5,6,7
		db	3,3,4,4,5,5,6,7
		db	3,3,3,4,5,5,6,7
		db	3,2,3,4,5,5,6,7
		db	3,2,3,4,5,5,6,7
		db	3,2,3,4,5,5,6,7
		db	2,2,3,4,5,5,6,7
		db	2,2,3,4,5,5,6,7
		db	2,2,3,3,4,5,6,7
		db	2,1,2,3,4,5,6,7
		db	1,1,2,3,4,5,6,7
		db	1,1,2,3,4,5,6,7
		db	1,1,2,3,4,5,6,7
		db	1,1,2,3,4,5,6,7
		db	0,1,2,3,4,5,6,7
		db	0,1,2,3,4,5,6,7


		align	256

pattern		incbin "pattern.bin"
screen		incbin "lucyshow.scr"
scratt = screen + #1800


		savebin "final.bin",start,$-start

tabmul = screen		;; Tabulka pre proporcionalnu zmenu farieb
tabsel = tabmul + #0100	;; Tabulka pre adresovanie paliet
tabpal = tabsel + #0100	;; 32 paliet pre 32 urovni jasov obrazku
vzoatt = tabpal + #2000	;; Skombinovane originalne atributy so vzorom
rutexe = vzoatt + #0600	;; Zobrazovaci engine

		display /D,(rutexe + #2000)," is end"

;;-----------------------------------------------------------------------------
	ifndef isFX
	opt push listoff
		define PRESET.border 6
		define PRESET.paper 0q066
		define PRESET.waitfor 5376
		include "../kernel/kernel.micro.inc"
		savesna "lucyshow.sna",microkernel
	opt pop
	endif
