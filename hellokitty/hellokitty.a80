		device	zxspectrum128

	ifdef isFX
		display "Compiling 'hellokitty'"
	endif

		include "../kernel/constants.inc"

;;-----------------------------------------------------------------------------
;; Busy soft ;; 18.02.2023 - 26.02.2022 ;; Kitty twister


	define	ROTATE	1	;; Celkove otacanie twisteru jednym smerom
	define	PATTERN_BANK 1

locate = #4020	;; Zaciatok efektu na obrazovke (lavy horny roh)
length = 176	;; Vyska efektu v pixeloch
colnot = 0q066	;; Atribut ked nic nie je vidno
col000 = 0q060	;; Cierna
col111 = 0q061	;; Modra
col222 = 0q062	;; Cervena
col333 = 0q063	;; Ruzova


		org	RUNZONE

start		ld	a,#10 + PATTERN_BANK
		call	xchg.bnk
		call	makula		;; Vytvorenie PUSHovacej rutinky
		call	makmat		;; Vytvorenie matematickej rutinky
		ld	a,colnot & 7
		ld	(actborder),a
		call	cls
		ld	a,#CD
		ld	hl,rutint
		ld	(IMCALL+1),hl
		ld	(IMCALL),a

		ld	bc,3455
.waitforit	ei
		halt
		ld	hl,(FRAMES)
		xor	a
		sbc	hl,bc
		jr	c,.waitforit


efekt1		ld	xh,col000

		ld	hl,#58C8
		ld	a,6
		call	ukaz

		ld	b,140
		call	wait

		ld	de,#5808
		ld	hl,#5988
		call	zmaz


efekt2		ld	xh,col111

		ld	b,28
		call	wait

		ld	hl,#59D0
		ld	a,6
		call	ukaz

		ld	b,140
		call	wait

		ld	de,#58F0
		ld	hl,#5AD0
		call	zmaz


efekt3		ld	xh,col222

		ld	b,28
		call	wait

		ld	hl,#5960
		ld	a,11
		call	ukaz

		ld	b,140
		call	wait

		ld	de,#5800
		ld	hl,#5AE0
		call	zmaz


efekt4		ld	xh,col111

		ld	hl,#5978
		ld	a,11
		call	ukaz

		ld	b,90
		call	wait

		ld	xh,col222

		ld	hl,#58C8
		ld	a,6
		call	ukaz

		ld	b,16
		call	wait

		ld	de,#5808
		ld	hl,#5988
		call	zmaz

		ld	de,#5818
		ld	hl,#5AF8
		call	zmaz


efekt5		ld	xh,col333

		ld	b,24
		call	wait

		ld	hl,#5970
		ld	a,11
		call	ukaz

		ld	b,24
		call	wait

		ld	xh,col222

		ld	hl,#5960
		ld	a,11
		call	ukaz

		ld	b,24
		call	wait

		ld	xh,col111

		ld	hl,#5968
		ld	a,11
		call	ukaz

		ld	b,24
		call	wait

		ld	xh,col000

		ld	hl,#5978
		ld	a,11
		call	ukaz

		ld	b,200
		call	wait


		ld	de,#5800
		ld	hl,#5AE0
		call	zmaz
		ld	de,#5808
		ld	hl,#5AE8
		call	zmaz
		ld	de,#5810
		ld	hl,#5AF0
		call	zmaz
		ld	de,#5818
		ld	hl,#5AF8
		call	zmaz

;; Ukoncenie efektu

		ld	a,#01		;; Zrusenie volania rutint
		ld	(IMCALL),a

cls		ld	hl,#4000
		ld	de,#4001
		ld	bc,#1800
		ld	(hl),l
		ldir
		ld	bc,#02FF
		ld	(hl),colnot
		ldir
		ret

wait		halt
		djnz	wait
		ret

;; Zmazanie jedneho stlpca
;;
;; HL = horny koniec
;; DE = spodny koniec

zmaz		push	hl
		xor	a
		sbc	hl,de
		pop	hl
		ret	c

		halt

		ld	a,colnot
	dup 7
		ld	(de),a
		inc	e
	edup
		ld	(de),a
		ld	bc,32 - 7
		ex	de,hl
		add	hl,bc
		ex	de,hl

		halt

		ld	a,colnot
	dup 7
		ld	(hl),a
		inc	l
	edup
		ld	(hl),a
		ld	bc,-(32 + 7)
		add	hl,bc

		jr	zmaz

;; Zobrazenie jedneho stlpca
;;
;; XH = Atribut pre zobrazenie
;; HL = Adresa laveho prostredneho atributu
;;  A = Vyska polovice slpca (zobrazi sa tolko atributov smerom hore aj dole)

ukaz		ld	d,h
		ld	e,l
		ld	bc,32
		add	hl,bc

ukalop		push	af

		ld	a,xh
	dup 7
		ld	(de),a
		inc	e
	edup
		ld	(de),a
		ld	bc,-(32 + 7)
		ex	de,hl
		add	hl,bc
		ex	de,hl

		halt

	dup 7
		ld	(hl),a
		inc	l
	edup
		ld	(hl),a
		ld	bc,32 - 7
		add	hl,bc

		halt

		pop	af
		dec	a
		jr	nz,ukalop
		ret

;; Casovo kriticky kod volany z prerusenia IMCALL

rutint
;; Vykreslenie na obrazovku - volanie PUSH:PUSH:PUSH...

pusher		ld	(pusret+1),sp
		jp	ulaexe
pusret		ld	sp,start

;; Matematika pre riadenie pohybu

matika
	if ROTATE
		ld	de,#0177
mvuho0		ld	hl,#1234
		add	hl,de
		ld	(mvuho0+1),hl
		ld	a,h
		ld	xl,a
	endif

		ld	de,#0139
mvuho1		ld	hl,#1234
		add	hl,de
		ld	(mvuho1+1),hl
		ld	b,h

		ld	de,#01BB
mvuho2		ld	hl,#1234
		add	hl,de
		ld	(mvuho2+1),hl

		ld	d,high tabsnh
		ld	e,h		;; DE = Pointer 1 to sinus table
		ld	l,b		;; HL = Pointer 2 to sinus table
		ld	h,d

		exx

mvpic0		ld	a,#55
		sub	#03
		ld	(mvpic0+1),a

		ld	de,#0133
mvpic1		ld	hl,#1234
		add	hl,de
		ld	(mvpic1+1),hl
		ld	l,h
		ld	h,high tabsin
		add	a,(hl)

		ld	de,#0259
mvpic2		ld	hl,#1234
		add	hl,de
		ld	(mvpic2+1),hl
		ld	l,h
		ld	h,high tabsin
		add	a,(hl)
		add	a,a
		and	#F8

		exx

		ld	c,a		;; C = Sprite line index
		ld	(movret+1),sp
		ld	sp,tabstk + 2*length
		jp	movexe
movret		ld	sp,#1234
		ret

makmat		ld	hl,matkus
		ld	de,movexe
		ld	bc,matlen
		push	de
		ldir
		ex	(sp),hl
		ld	bc,matlen * (length - 1)
		ldir
		pop	hl
		ld	c,#03
		ldir
		ret

matkus		ld	a,#F8
		add	a,c
		ld	c,a
		ld	a,(de)
		add	a,(hl)
	if ROTATE
		add	a,xl
	endif
		inc	e
		inc	l
		or	#C0
		ld	b,a
		push	bc
matjmp		jp	movret

matlen = matjmp-matkus

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Vytvorenie zobrazovacej rutinky PUSH:PUSH:PUSH...

makula		ld	xh,length
		ld	hl,ulaexe
		ld	de,locate + 32
		ld	bc,tabstk
mkulop		ld	(hl),#ED	;; LD SP,(...)
		inc	hl
		ld	(hl),#7B
		inc	hl
		ld	(hl),c
		inc	hl
		ld	(hl),b
		inc	hl
		ld	a,#C1		;; POP BC
mkupop		ld	(hl),a
		inc	hl
		add	a,#10
		jr	nc,mkupop

		ld	(hl),#31
		inc	hl
		ld	(hl),e
		inc	hl
		ld	(hl),d
		inc	hl

		ld	xl,#04
mkupus		ld	a,#F5		;; PUSH AF
mkupsh		ld	(hl),a
		inc	hl
		sub	#10
		cp	#C0
		jr	nc,mkupsh
		dec	xl
		jr	nz,mkupus

		ex	de,hl
		dec	hl
		call	downhl
		inc	hl
		ex	de,hl

		inc	bc
		inc	bc

		dec	xh
		jr	nz,mkulop

		ld	(hl),#C3	;; JP ...
		inc	hl
		ld	(hl),low pusret
		inc	hl
		ld	(hl),high pusret
		ret

downhl		inc	h
		ld	a,h
		and	7
		ret	nz
		ld	a,32
		add	a,l
		ld	l,a
		ret	c
		ld	a,-8
		add	a,h
		ld	h,a
		ret


		savebin "final.bin",start,$-start

;;-----------------------------------------------------------------------------

		align	256

tabsyn		ds	#0100		;; Sinus tabulka: Dvojita presnost     0-255-0-255-0
tabsin		ds	#0100		;; Sinus tabulka: Normalna znamienkova 128-255-128-0-128
tabsnh		ds	#0100		;; Sinus tabulka: Polovicna zamienkova 64-127-64-0-64
		ds	#0100		;; Rezerva pre istotu ak by pretiekol stack pri plneni tabstk
tabstk		ds	#0200		;; Matematikou vypocitane polohy jednotlivych dielikov twisteru

ulaexe		ds	#1500		;; Zobrazovani PUSH:PUSH:PUSH lopatovac do obrazovky
movexe		ds	#0B00		;; Matematika pocitajuca polohy dielikov twisteru


tabmap = #C000		;; 16kB picture remapped for twister in bank 1

		org	tabsyn
		incbin "sin-table.bin"


		savebin "final.bin",start,$-start

;;-----------------------------------------------------------------------------
	ifndef isFX
	opt push listoff
		page PATTERN_BANK
		org tabmap
		incbin "pattern.bin"

		define PRESET.border 0
		define PRESET.paper 0q000
		define PRESET.waitfor 3452
		include "../kernel/kernel.micro.inc"
		savesna "hellokitty.sna",microkernel
	opt pop
	endif
